; Generated by JITX 3.24.1
#use-added-syntax(jitx)
defpackage ethernet-io/power :
  import core
  import jitx
  import jitx/commands
  import jitx/parts

  import helpers
  import jsl

public pcb-module power-management :
  port VDD-3v3 : power
  port VDD-USB : power
  port VDD-1v2 : power

  val buck-5V-cst = power-systems/DC-DC/buck/BuckConstraints(
    v-in = min-max(20.0, 28.0),
    v-out = 5.5 +/- (2 %)
    v-in-ripple-max = 0.050
    v-out-ripple-max = 0.030
    i-out = 1.0 +/- (20 %)
    freq = 1.2e6
    K = (40 %)
  )

  inst DCDC-5V : TI-vreg/components/TPS6293x/circuit(
    TI-vreg/components/TPS6293x/TPS62932DRL,
    buck-5V-cst,
    freq = 1.2e6,
    SS-period = 10.0e-3 +/- 2.0e-3,
    UVLO = [15.0, 12.0]
  )

  val cxt-3v3 = power-systems/DC-DC/buck/BuckConstraints(
    v-in = min-max(4.9, 5.5)
    v-out = 3.3 +/- (3 %)
    v-in-ripple-max = 0.050
    v-out-ripple-max = 0.030
    i-out = 1.0 +/- (20 %)
    freq = 1.2e6
    K = (40 %)
  )

  inst DCDC-3v3 : TI-vreg/components/TPS6208x/circuit(
    TI-vreg/components/TPS6208x/TPS62082DSG
    cxt-3v3
    snooze-mode = false
    snooze-conn? = create-resistor(
        resistance = 0.0,
        precision = (1 %)
      )
    )

  val cxt-1v2 = power-systems/DC-DC/buck/BuckConstraints(
    v-in = min-max(4.9, 5.5)
    v-out = 1.2 +/- (5 %)
    v-in-ripple-max = 0.050
    v-out-ripple-max = 0.030
    i-out = 1.0 +/- (20 %)
    freq = 1.2e6
    K = (40 %)
  )

  inst DCDC-1v2 : TI-vreg/components/TPS6208x/circuit(
    TI-vreg/components/TPS6208x/TPS62082DSG
    cxt-1v2
    snooze-mode = false
    snooze-conn? = create-resistor(
        resistance = 0.0,
        precision = (1 %)
      )
    )

  inst USB-OR : power-systems/filters/diode-OR(diodes/SSA33L/component, 2)
  net (USB-OR.vin[0] VDD-USB)
  net (USB-OR.vin[1] DCDC-5V.conv.VOUT)
  net (USB-OR.vout DCDC-3v3.conv.VIN DCDC-1v2.conv.VIN)

  net (DCDC-3v3.conv.VOUT VDD-3v3)
  net (DCDC-1v2.conv.VOUT VDD-1v2)

  ; 1.2V must come up before 3.3V
  net (DCDC-1v2.enable USB-OR.vout.V+)

  net (DCDC-1v2.power-good DCDC-3v3.enable)
  insert-pullup(DCDC-3v3.enable USB-OR.vout)

  inst terminal-block : connectors/components/LSF-SMT/component(num-poles = 4)
  net P24V (terminal-block.p[1] terminal-block.p[2] DCDC-5V.conv.VIN.V+)
  net GND (terminal-block.p[3] terminal-block.p[4] DCDC-5V.conv.VIN.V- VDD-USB.V-)